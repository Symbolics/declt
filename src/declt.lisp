;;; declt.lisp --- Entry points

;; Copyright (C) 2010 Didier Verna

;; Author:        Didier Verna <didier@lrde.epita.fr>
;; Maintainer:    Didier Verna <didier@lrde.epita.fr>
;; Created:       Mon Aug 23 17:47:05 2010
;; Last Revision: Mon Aug 23 17:47:05 2010

;; This file is part of Declt.

;; Declt is free software; you can redistribute it and/or modify
;; it under the terms of the GNU General Public License as published by
;; the Free Software Foundation; either version 2 of the License, or
;; (at your option) any later version.

;; Declt is distributed in the hope that it will be useful,
;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;; GNU General Public License for more details.

;; You should have received a copy of the GNU General Public License
;; along with this program; if not, write to the Free Software
;; Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.


;;; Commentary:

;; Contents management by FCM version 0.1.


;;; Code:

(in-package :com.dvlsoft.declt)


(defun begin-texi-file (name infofile subtitle version author email)
  "Write the header of the Texinfo file.
- NAME is the reference manual name.
- INFOFILE is the info file basename sans extension.
- SUBTITLE is a subtitle for the title page."
  (format t "\\input texinfo

@c %**start of header
@setfilename ~A.info
@settitle The ~A Reference Manual
@afourpaper
@c %**end of header

@setcontentsaftertitlepage
@documentdescription
This is the Reference Manual for ~A.
This manual was automatically generated by Declt.
@end documentdescription

@dircategory Common Lisp
@direntry
* ~A Reference: (~A). The ~A Reference Manual.
@end direntry

@titlepage
@title The ~A Reference Manual
~A~A
@c @page
@c @vskip 0pt plus 1filll
@end titlepage

@contents

@ifnottex
@node Top, Reference, (dir), (dir)
@top The ~A Reference Manual

This is the Reference Manual for ~A.
This manual was automatically generated by Declt.

@menu
* Reference::	The reference manual
* Indexes::	Functions, variables and data types
@end menu
@end ifnottex

@node Reference, Indexes, Top, Top
@chapter Reference

"
    infofile name name name infofile name name
    (if (or subtitle version)
	(format nil "@subtitle ~@[~A~]~:[~;, ~]~@[version ~A~]~%"
	  subtitle (and subtitle version) version)
      "")
    (if (or author email)
	(format nil "@author ~@[~A~]~:[~; ~]~@[<@email{~A}>~]~%"
	  author (and author email) email)
      "")
    name name))

(defun end-texi-file (basename)
  "Write the footer of the Texinfo file.
- BASENAME is the file's basename sans extension."
  (format t "@node Indexes
@chapter Indexes

@menu
* Function Index::
* Variable Index::
* Data Type Index::
@end menu

@node Function Index, Variable Index, Indexes, Indexes
@section Functions
@printindex fn
@page

@node Variable Index, Data Type Index, Function Index, Indexes
@section Variables
@printindex vr
@page

@node Data Type Index, , Variable Index, Indexes
@section Data Types
@printindex tp

@bye

@c ~A ends here"
    basename))

(defun declt (system-name
	      &key (name (string-downcase (symbol-name system-name)))
		   (texifile (format nil "~A.texi" name))
		   (infofile (pathname-name texifile))
		   (subtitle nil subtitlep)
		   (version nil versionp)
		   (author nil authorp)
		   (email nil emailp)
	      &aux (system (asdf:find-system system-name)))
  "Generate a reference manual in Texinfo format for ASDF SYSTEM-NAME.
- NAME is the library name (defaults to SYSTEM-NAME).
- TEXIFILE is where to store the manual (defaults to NAME.texi).
- INFOFILE is the info file basename sans extension
  (defaults to that of TEXIFILE).
- SUBTITLE and VERSION are for the @subtitle command (default to the system's
  description and version).
- AUTHOR and EMAIL are for the @author command (default to the system's
  author)."
  (unless subtitlep
    (setq subtitle (asdf:system-description system))
    (when (char= (aref subtitle (1- (length subtitle))) #\.)
      (setq subtitle (subseq subtitle 0 (1- (length subtitle))))))
  (unless versionp
    (setq version (asdf:component-version system)))
  (multiple-value-bind (system-author system-email)
      (parse-author-string (asdf:system-author system))
    (unless authorp
      (setq author system-author))
    (setq email (escape (if emailp email system-email))))
  (with-open-file (*standard-output* texifile
		   :direction :output
		   :if-exists :supersede
		   :if-does-not-exist :create)
    (begin-texi-file name infofile subtitle version author email)
    (end-texi-file (make-pathname :name (pathname-name texifile)
				  :type (pathname-type texifile))))
  (values))


;;; declt.lisp ends here
